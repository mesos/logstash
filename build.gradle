buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter {
            url "http://jcenter.bintray.com/"
        }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.5.0'
    }
}

import com.github.jengelman.gradle.plugins.shadow.*

allprojects {
    apply plugin: 'idea'
}

idea {
    project {
        languageLevel = '1.8'
        ipr {
            withXml { provider ->
                provider.node.component
                        .find { it.@name == 'VcsDirectoryMappings' }
                        .mapping.@vcs = 'Git'
            }
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'application'

    //apply from: "$rootDir/gradle/quality.gradle"
    //apply from: "$rootDir/gradle/spock.gradle"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    group = "com.apache.mesos.logstash"
    version = "0.1.0"

    ext {
        mesosVer = "0.22.1"
        slf4jVer = "1.7.12"
        logbackVer = "1.1.3"
        springBootVer = "1.2.5.RELEASE"
        junitVer = "4.11"
        awaitilityVer = "1.6.3"
        mockitoVer = "1.9.5"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://jitpack.io"
        }
    }

    dependencies {
        compile "org.apache.mesos:mesos:${mesosVer}"

        runtime "org.slf4j:log4j-over-slf4j:${slf4jVer}"
        runtime "ch.qos.logback:logback-classic:${logbackVer}"

        testCompile "org.hamcrest:hamcrest-core:1.3"
        testCompile "junit:junit-dep:${junitVer}"
        testCompile "com.jayway.awaitility:awaitility:${awaitilityVer}"
        testCompile "org.mockito:mockito-all:${mockitoVer}"
    }
}

