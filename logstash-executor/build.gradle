apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = "org.apache.mesos.logstash.executor.Application"
ext {
    imageName = imagePrefix + '/logstash-executor'
}

dependencies {
    compile project(':logstash-commons')
    compile "ch.qos.logback:logback-classic:${logbackVer}"
    compile "org.apache.commons:commons-compress:$commonsCompressVer"
    compile "org.apache.commons:commons-io:1.3.2"
    compile "com.google.guava:guava:$guavaVer"
    compile 'org.freemarker:freemarker:2.3.23'

}

shadowJar {
    baseName = "logstash-mesos-executor"
    mergeServiceFiles()
    classifier = ''
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

task taskCopyFilesForDocker(type: Copy) {
    dependsOn "copyJar"
}

task copyJar(type: Copy) {
    dependsOn 'shadowJar'
    from { getTasksByName("shadowJar", false)[0].outputs.files[0] } // Get outputs from task "jar"
    from        "start-executor.sh"
    into        'build/docker'
    rename { String fileName ->
        fileName.replace("-${project.version}", "")
    }
}
