apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'groovy'

mainClassName = "org.apache.mesos.logstash.executor.LogstashExecutor"

sourceCompatibility = 1.7
version = '1.0'

evaluationDependsOn(':local')

buildscript {
    repositories { jcenter { url "http://jcenter.bintray.com/" } }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:2.2'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    }
}

run {

    args "-m", "192.168.1.106:5050", "-f", "./conf.conf"
    jvmArgs "-Djava.library.path=/usr/local/lib"
}

repositories {
    mavenCentral()
}

dependencies {

    compile project(':local')

    compile 'org.apache.mesos:mesos:0.22.1'
    compile "log4j:log4j:1.2.16"
    compile 'com.github.docker-java:docker-java:1.3.0'
    compile 'com.spotify:docker-client:2.7.7'
    compile 'org.freemarker:freemarker:2.3.20'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile "org.mockito:mockito-core:1.10.19"
    testCompile 'com.jayway.awaitility:awaitility:1.6.3'
}

shadowJar {
    baseName = "logstash-executor-dependencies"

    exclude 'org/apache/mesos/logstash' // only include dependencies
}

jar {
    baseName = "logstash-executor"
    manifest {
        attributes 'Main-Class': 'org.apache.mesos.logstash.executor.LogstashExecutor',
                'Class-Path': 'logstash-executor-dependencies.jar'
    }
}

task copyJar(type: Copy) {
    dependsOn 'shadowJar', 'jar'

    from "build/libs/logstash-executor-${project.version}.jar"
    from "build/libs/logstash-executor-dependencies-${project.version}-all.jar"

    into 'build/docker'

    rename { String fileName ->
        fileName.replace("-${project.version}-all", "").replace("-${project.version}", "")
    }
}


task buildDockerImage(type: Exec) {
    dependsOn copyJar

    executable "docker"
    args "build"
    args "-t"
    args project(':local').executorImageName
    args "."
}

task pushDockerImage(type: Exec) {
    dependsOn buildDockerImage

    executable "docker"
    args "push"
    args project(':local').executorImageName
}

build.dependsOn copyJar
build.dependsOn pushDockerImage
