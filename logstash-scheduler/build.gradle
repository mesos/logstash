apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'spring-boot'
apply plugin: 'de.undercouch.download'

mainClassName = "org.apache.mesos.logstash.scheduler.Application"
ext {
    imageName = imagePrefix + '/logstash-scheduler'
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'de.undercouch:gradle-download-task:2.1.0'
    }
}

repositories {
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    compile project(':logstash-commons')

    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile("org.springframework.boot:spring-boot-starter-logging")
    compile("org.springframework.boot:spring-boot-starter-websocket")
    compile("org.springframework:spring-messaging")
    compile('com.github.containersolutions.mesos-starter:spring-boot-starter-mesos:0.1')

    testCompile("org.springframework:spring-test")
}

task copyExecutor(type: Copy) {
    from { project(":logstash-executor").getTasksByName("copyJar", false)[0].outputs.files[0] } // Include executor
    into "$buildDir/resources/main/static"
    rename { String fileName ->
        fileName.replace("-${project.version}", "")
    }
}

jar {
    dependsOn 'getLogstashTarball', 'copyExecutor'
    baseName = "logstash-mesos-scheduler"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } } // Include dependencies

    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'

    manifest {
        attributes(
                'Main-Class': mainClassName,
                'Implementation-Version': project.version
        )
    }
}

import de.undercouch.gradle.tasks.download.Download
task getLogstashTarball(type: Download) {
    src "https://download.elastic.co/logstash/logstash/logstash-" + "$logstashVer" + ".tar.gz"
    dest new File(buildDir, './resources/main/public/logstash.tar.gz')
    onlyIfNewer true
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

test {
    testLogging {
        showStandardStreams = true
    }
}

task taskCopyFilesForDocker(type: Copy) {
    dependsOn "copyJar"
}

task copyJar(type: Copy) {
    dependsOn   'copyExecutor'
    from        "build/libs/logstash-mesos-scheduler-${project.version}.jar"
    from        "start-scheduler.sh"
    into        'build/docker'
    rename { String fileName ->
        fileName.replace("-${project.version}", "")
    }
}
